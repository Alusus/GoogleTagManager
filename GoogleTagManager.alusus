module GoogleTagManager {
    @assetsRoute["/gtagmanager/"] def assetsRoute: ("$Assets/", "GtagManagerAssets/");

    function initializeFrontend(id: CharsPtr): Srl.SrdRef[Promises.Promise[Int]] {
        return loadJsScript(String.format("https://www.googletagmanager.com/gtag/js?id=%s", id))
            .then[Int](closure (Int, p: ref[Promises.Promise[Int]]) {
                p.resolve(loadJsScript("/gtagmanager/binding.js"));
            }).then[Int](closure (Int, p: ref[Promises.Promise[Int]]) {
                callCustomJsFn("configureGoogleTagManager", id);
                p.resolve(0);
            });
    }

    function pushEvent(eventName: CharsPtr, jsonArg: CharsPtr) {
        def args: String = String.format("[\"event\", \"%s\", %s]", eventName, jsonArg);
        callCustomJsFn("pushGoogleTagEvent", args);
    }

    function pushConversionEvent(id: String, conversionId: String, transactionId: String) {
        def args: String = String.format(
            "[\"event\", \"conversion\", {\"send_to\": \"%s/%s\", \"transaction_id\": \"%s\"}]",
            Json.escape(id).buf,
            Json.escape(conversionId).buf,
            Json.escape(transactionId).buf
        );
        callCustomJsFn("pushGoogleTagEvent", args);
    }
}
